# 输入和输出

## 输入和输出控制硬件

- 显示器
- 显卡
- 键盘

端口（in/out） / 映射内存

Intel 0-65535个端口: 0x0000 - 0xFFFF

```vda操作端口
阴极射线管控制器：Cathode Ray Tube Controller

CRT 地址端口 0x3D4
CRT 数据端口 0x3D5
```

BIOS会初始化显示器为80 * 25 = 2000个字符，光标表示的就是第几个字符的位置。

```
0x0E 光标位置高8位
0x0F 光标位置低8位
```

```asm
in al, dx; dx为端口号
; in ah, dx 没有这种写法！！！
in ax，dx; ax/al为数据
out dx, al; dx为端口号
out dx, ax; ax/al为数据
```

```
CRT_ADDR_REG    equ 0x3D4
CRT_DATA_REG    equ 0x3D5
CRT_CURSOR_HIGH equ 0x0E
CRT_CURSOR_LOW  equ 0x0F

set_cursor_pos:
    ; 设置光标位置，传入参数在ax中
    ; 设置光标的低8位
    push dx
    push bx
    mov bx, ax
    mov dx, CRT_ADDR_REG
    mov al, CRT_CURSOR_LOW
    out dx, al

    mov dx, CRT_DATA_REG
    mov al, bl
    out dx, al

    ; 设置光标的高8位
    mov dx, CRT_ADDR_REG
    mov al, CRT_CURSOR_HIGH
    out dx, al

    mov dx, CRT_DATA_REG
    mov al, bh
    out dx, al

    pop bx
    pop dx
    ret

get_cursor_pos:
    ; 获取光标位置，返回值存储在ax中
    push dx
    ; 获取光标的高8位
    mov dx, CRT_ADDR_REG
    mov al, CRT_CURSOR_HIGH
    out dx, al

    mov dx, CRT_DATA_REG
    in al, dx

    shl ax, 8

    ; 获取光标的低8位
    mov dx, CRT_ADDR_REG
    mov al, CRT_CURSOR_LOW
    out dx, al

    mov dx, CRT_DATA_REG
    in al, dx

    pop dx
    ret
```