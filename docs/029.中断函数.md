# 中断函数（实模式）

## 简介

- 内中断
    - 软中断
        - 系统调用：读写文件
    - 异常
        - 除零异常
        - 指令错误：无法解释的指令
        - 缺页异常

- 外中断
    - 时钟中断
    - 键盘中断
    - 硬盘中断
        - 同步端口I/O：最开始我们读硬盘的时候使用了超时指令，目的就是为了防止硬盘还没有读完就走到下面的处理数据的程序里了。
        - 异步端口I/O：等到硬盘中断到来之后再去处理数据。
        - DMA：直接内存访问，硬盘直接把数据写到内存里，不需要CPU参与。

## 中断函数

普通函数调用使用的是`call`和`ret`指令，而中断函数调用使用的是`int`和`iret`指令。

```asm
int 0x80; 表示引发了第128号中断函数
```

call指令会将`eip`压入栈中，而int指令会将`eip/cs/eflags`压入栈中。
ret指令会将栈顶的值赋给`eip`，而iret指令会将栈顶的值赋给`eip/cs/eflags`。

cs: 函数会在任何位置触发中断，因此需要将触发中断的位置的cs和eip压入栈中，以便在中断函数返回的时候能够正确的返回到触发中断的位置。
eflag：为了屏蔽外中断的影响，需要将eflag压入栈中，以便在中断函数返回的时候能够正确的返回到触发中断的位置。

## 中断向量表

中断向量就是中断函数的指针

> `0x00000000`到`0x000003ff` 1KB的空间是保留给中断向量表的。总共1KB/4B=256个中断函数指针。

## 实模式中断代码

```s
[org 0x7c00]

; 设置屏幕模式为文本模式，清除屏幕
mov ax, 3
int 0x10

; 初始化段寄存器
mov ax, 0
mov ds, ax
mov es, ax
mov ss, ax
mov sp, 0x7c00

xchg bx, bx
; call interrupt ; 普通调用

mov word [0 * 4], interrupt ; ip
mov word [0 * 4 + 2], 0     ; cs

; int 0x80; linux 系统调用号 invoke

mov dx, 0
mov ax, 1
mov bx, 0

xchg bx, bx

div bx ; dx : ax / bx ； 触发除零异常，陷入0号中断函数


; 阻塞
jmp $

interrupt:
    mov si, string
    call print
    xchg bx, bx
    ; ret ;  普通返回
    iret ; 中断返回

print:
    mov ah, 0x0e
.next:
    mov al, [si]
    cmp al, 0
    jz .done
    int 0x10
    inc si
    jmp .next
.done:
    ret

string:
    db ".", 0

; 填充 0
times 510 - ($ - $$) db 0

; 主引导扇区的最后两个字节必须是 0x55 0xaa
; dw 0xaa55
db 0x55, 0xaa
```

